"""Test different optimization settingss."""


import itertools
import pickle
import sys
import time

import pandas as pd

import polyclonal


# define parameters to explore
params_to_test = {
    'noise': ['exact', 'noisy'],
    'sitefirst': ['sitefirst', 'nosite'],
    'collapse': ['collapsed', 'uncollapsed'],
    'reg_escape_weight': [0, 0.01, 0.05, 0.25, 1],
    'reg_spread_weight': [0, 0.01, 0.05, 0.25, 1],
    }
params_to_test_df = pd.DataFrame.from_records(
                            list(itertools.product(*params_to_test.values())),
                            columns=params_to_test,
                            )
paramspace = snakemake.utils.Paramspace(params_to_test_df)


rule all:
    input:
        expand("results/opt_settings/{params}.csv", params=paramspace.instance_patterns)

rule prep_variants_df:
    input: csv="../notebooks/RBD_variants_escape_{noise}.csv"
    output: csv="results/opt_settings/variants_{noise}.csv"
    run:
        (pd.read_csv(input.csv, na_filter=None)
         .query('library == "avg2muts"')
         .query('concentration in [0.25, 1, 4]')
         .to_csv(output.csv, index=False)
         )

rule optimize:
    input: variants_df_csv=rules.prep_variants_df.output.csv
    output:
        csv=f"results/opt_settings/{paramspace.wildcard_pattern}.csv",
        pickle=f"results/opt_settings/{paramspace.wildcard_pattern}.pickle",
    log: log=f"results/opt_settings/{paramspace.wildcard_pattern}.log",
    run:
        with open(log.log, 'w') as f:
            variants_df = pd.read_csv(input.variants_df_csv, na_filter=None)
            poly_abs = polyclonal.Polyclonal(
                    data_to_fit=variants_df,
                    activity_wt_df=pd.DataFrame.from_records(
                            [('epitope 1', 3.0),
                             ('epitope 2', 2.0),
                             ('epitope 3', 1.0),
                             ],
                            columns=['epitope', 'activity'],
                            ),
                    site_escape_df=pd.DataFrame.from_records(
                            [('epitope 1', 484, 10.0),
                             ('epitope 2', 446, 10.0),
                             ('epitope 3', 417, 10.0),
                             ],
                            columns=['epitope', 'site', 'escape'],
                            ),
                    data_mut_escape_overlap='fill_to_data',
                    collapse_identical_variants='mean' if wildcards.collapse == 'collapsed' else False,
                    )
            start_time = time.time()
            opt_res = poly_abs.fit(
                    reg_escape_weight=float(wildcards.reg_escape_weight),
                    reg_spread_weight=float(wildcards.reg_spread_weight),
                    log=f,
                    logfreq=5,
                    fit_site_level_first=True if wildcards.sitefirst == 'sitefirst' else False,
                    )
            elapsed_time = time.time() - start_time
            print(opt_res)
            print(f"Writing results to {output.csv}")
            with open(output.csv, 'w') as f:
                f.write(f"loss,time\n{opt_res.fun:.5g},{elapsed_time:.5g}\n")
            print(f"Writing model to {output.pickle}")
            with open(output.pickle, 'wb') as f:
                pickle.dump(poly_abs, f)
